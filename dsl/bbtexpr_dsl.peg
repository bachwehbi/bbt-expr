{
  var bbtCondition = require('./condition.js');
  var types = require('./types.js');
  var moment = require('moment');
  var Geolib = require('geolib');
  var mathjs = require('mathjs');
}

start
  = c_condition:OrExpression { return c_condition; }
  / Additive:Additive {return Additive;}

OrExpression
  = left:AndExpression right:(OR ws exp:OrExpression {return exp;})? {
    if (right) {
      return bbtCondition.createExpressionOrValue({
        left: left,
        right: right,
        oper: types.OPERATORS.BBT_OR
      })
    } else {
      return left;
    }
  }

AndExpression
  = left:atom right:(AND ws exp:AndExpression {return exp})? {
    if (right) {
      return bbtCondition.createExpressionOrValue({
        left: left,
        right: right,
        oper: types.OPERATORS.BBT_AND
      })
    } else {
      return left
    }
  }

atom
  = ws '(' ws exp:OrExpression ws ')' ws {
    return exp;
  }
  / NOT ws atom:atom {
    return bbtCondition.createExpressionOrValue({
      left: atom,
      oper: types.OPERATORS.BBT_NOT
    })
  }
  / Comparison:Comparison ws {
    return Comparison;
  }

ComparisonOperator
  = "==" {return types.OPERATORS.BBT_EQ}
  / "!=" {return types.OPERATORS.BBT_NEQ}
  / ">" {return types.OPERATORS.BBT_GT}
  / ">=" {return types.OPERATORS.BBT_GTE}
  / "<" {return types.OPERATORS.BBT_LT}
  / "<=" {return types.OPERATORS.BBT_LTQ}

Comparison
  = left:Additive ws oper:ComparisonOperator ws right:Additive {
    return bbtCondition.createExpressionOrValue({
      left: left,
      right: right,
      oper: oper
    })
  }

AdditiveOperator
  = "+" {return types.OPERATORS.BBT_ADD}
  / "-" {return types.OPERATORS.BBT_SUB}

Additive
  = left:Multiplicative  ws oper:AdditiveOperator  ws right:Additive {
    return bbtCondition.createExpressionOrValue({
      left: left,
      right: right,
      oper: oper
    })
  }
  / Multiplicative

MultiplicativeOperator
  = "*" { return types.OPERATORS.BBT_MULT }
  / "/" { return types.OPERATORS.BBT_DIV }
  / "idiv" { return types.OPERATORS.BBT_IDIV }
  / "%" { return types.OPERATORS.BBT_MOD }

Multiplicative
  = left:NestedValue  ws oper:MultiplicativeOperator  ws right:Multiplicative {
    return bbtCondition.createExpressionOrValue({
      left: left,
      right: right,
      oper: oper
    })
  }
  / NestedValue

NestedValue
  = value:value keys:keys? {
    if (value.keys) {
      value.keys.concat(keys)
    } else {
      value.keys = keys;
    }

    return value;
  }

value =
  OPEN_PARENTHESE  ws Additive:Additive ws CLOSE_PARENTHESE {
    return Additive
  }
  / primitive:primitive {
    return new bbtCondition.Value({
      val: primitive
    })
  }
  / constructor:constructor {return constructor;}
  / trigger:trigger {return new bbtCondition.TriggerValue()}
  / resource:resource
  {
    return new bbtCondition.ResourceValue(resource);
  }
  / fct:fct {
    return new bbtCondition.FunctionValue(fct)
  }

fct =
  strfct:strfct
  / mathfct:mathfct
  / geofct:geofct

strfct =
  CONCAT OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: function() {
          return String.prototype.concat.apply("", arguments)
        },
        params: params
      };
    }
  / SUBSTRING OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length < 2 || params.length > 3) {
        throw 'Arguments error: substring accepts 2 or 3 arguments';
      }
      return {
        fct: function(s, indexStart, indexEnd) {
          return String.prototype.substring.apply(s, [indexStart, indexEnd])
        },
        params: params
      };
    }

  / STARTSWITH OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length != 2) {
        throw 'Arguments error: startsWith takes exactly 2 arguments';
      }
      return {
        fct: function(str, prefix) {
          return str.startsWith(prefix)
        },
        params: params
      };
    }
  / ENDSWITH OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length != 2) {
        throw 'Arguments error: endsWith takes exactly 2 arguments';
      }
      return {
        fct: function(str, suffix) {
          return str.endsWith(suffix)
        },
        params: params
      };
    }
  / INCLUDES OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length != 2) {
        throw 'Arguments error: endsWith takes exactly 2 arguments';
      }
      return {
        fct: function(str, lup) {
          return str.includes(lup)
        },
        params: params
      };
    }

mathfct =
  MIN OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.min,
        params: params
      };
    }
  / MAX OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.max,
        params: params
      };
    }

  / MEAN OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.mean,
        params: params
      };
    }
  / RANDOM OPEN_PARENTHESE CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.random
      };
    }
  / CEIL OPEN_PARENTHESE parameter:Parameter CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.ceil,
        params: [parameter]
      };
    }
  / FLOOR OPEN_PARENTHESE parameter:Parameter CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.floor,
        params: [parameter]
      };
    }
  / ABS OPEN_PARENTHESE parameter:Parameter CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.abs,
        params: [parameter]
      };
    }
  / SQRT OPEN_PARENTHESE parameter:Parameter CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.sqrt,
        params: [parameter]
      };
    }
  / POW OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.pow,
        params: params
      };
    }
  / MEDIAN OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.median,
        params: params
      };
    }
  / SUM OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.sum,
        params: params
      };
    }
  / STD OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: mathjs.std,
        params: params
      };
    }

geofct =
  GETDISTANCE OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length != 2) {
        throw 'Arguments error: getDistance takes exactly 2 arguments';
      }
      return {
        fct: Geolib.prototype.getDistance,
        params: params
      };
    }
  / ISPOINTINSIDE OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length != 2) {
        throw 'Arguments error: isPointInside takes exactly 2 arguments';
      }
      return {
        fct: Geolib.prototype.isPointInside,
        params: params
      };
    }
  / ISPOINTINCIRCLE OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length != 3) {
        throw 'Arguments error: isPointInCircle takes exactly 3 arguments';
      }
      return {
        fct: Geolib.prototype.isPointInCircle,
        params: params
      };
    }
  / ISPOINTINLINE OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      if (params.length != 3) {
        throw 'Arguments error: isPointInLine takes exactly 3 arguments';
      }
      return {
        fct: Geolib.prototype.isPointInLine,
        params: params
      };
    }
  / GETCENTER OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: Geolib.prototype.getCenter,
        params: params
      };
    }
  / GETPATHLENGTH OPEN_PARENTHESE params:ParameterList CLOSE_PARENTHESE
    {
      return {
        fct: Geolib.prototype.getPathLength,
        params: params
      };
    }

resource = channel:STNAME "." resource:STNAME {
  return {
    channel: channel,
    resource: resource
    }
  }

trigger = TRIGGER

operation = key

keys =
  key+

key =
  "." elem:STNAME {return elem;}

ParameterList
  = param:Parameter params:(',' parameter:Parameter {return parameter;})* {
    if (params.length) {
      params.unshift(param);
      return params
    } else {
      return [param];
    }
  }


Parameter =
  ws param:Additive ws {return param;}

constructor =
  dateconstructor:date_constructor {return dateconstructor;}
  / gps:geopoint {return gps;}
  / poly:polygone {return poly;}

date_constructor =
  DATE "(" params:ParameterList? ")"
    {
      return new bbtCondition.FunctionValue({
        fct: function() {
          var bound = moment.bind();
          var retval = bound.apply(undefined, arguments)
          console.log(retval.format())
          return retval
        },
        params: params
      });
    }

geopoint =
  ws GEOPOINT OPEN_PARENTHESE lat:Parameter ws ',' ws lng:Parameter CLOSE_PARENTHESE
    {
      return new bbtCondition.FunctionValue({
        fct: function(latitude, longitude) {
          return {latitude: latitude, longitude: longitude}
        },
        params: [lat, lng]
      });
      return
    }
  / ws GEOPOINT OPEN_PARENTHESE parameter:Parameter ws CLOSE_PARENTHESE
    {
      return new bbtCondition.FunctionValue({
        fct: function(param) {
          // we are expecting param to contain latitude and longitude
          if (!param.hasOwnProperty('latitude') || param.hasOwnProperty('longitude')) {
            throw new Error('Expecting param to have latitude and longitude parameters')
          }

          return {latitude: param.latitude, longitude: param.longitude}
        },
        params: [parameter]
      });
      return
    }


polygone
  = ws POLYGONE OPEN_PARENTHESE p1:geopoint ',' p2:geopoint points:(',' point:geopoint {return point;})+ CLOSE_PARENTHESE {
    points.unshift(p1, p2)
    return new bbtCondition.FunctionValue({
      fct: function(pts) {
        return Array.prototype.slice.call(arguments);
      },
      params: points
    });
  }

primitive
  = str:string {return str;}
  / nb:number {return nb;}
  / b:bool {return b;}

bool
  = "true"i {return true;}
  / "false"i {return false;}

OR
  = "OR"i /* case insensitive */
  / "||"

AND
  = "AND"i /* case insensitive */
  / "&&"

NOT
  = "NOT"i /* case insensitive */
  / '!'

OPEN_PARENTHESE = '('
CLOSE_PARENTHESE = ')'

STRING = "String"
DATE = "Date"
GEOLIB = "Geolib"
GEOPOINT = "Geopoint"
POLYGONE = "Polygone"
MATH = "Math"
TRIGGER = "trigger"i

MIN = "min"i
MAX = "max"i
MEAN = "mean"i
RANDOM = "random"i
CEIL = "ceil"i
FLOOR = "floor"i
ABS = "abs"i
SQRT = "sqrt"i
POW = "pow"i
MEDIAN = "median"i
SUM = "sum"i
STD = "std"i


STARTSWITH = "startswith"i
ENDSWITH = "endswith"i
CONCAT = "concat"i
SUBSTRING = "substring"i
INCLUDES = "includes"i

GETDISTANCE = "getdistance"i
ISPOINTINSIDE = "ispointinside"i
GETCENTER = "getcenter"i
ISPOINTINCIRCLE = "ispointincircle"i
GETPATHLENGTH = "getpathlength"i
ISPOINTINLINE = "ispointinline"i

STNAME = char:[a-zA-Z_] chars:[0-9a-zA-Z_]* { return char + chars.join(""); }

string "string" =
  '"' chars:chars '"' { return chars; }
  / STRING OPEN_PARENTHESE value:primitive CLOSE_PARENTHESE
    {
      return String(value)
    }

chars
  = chars:char+ { return chars.join(""); }

char
  // In the original JSON grammar:
  // "any-Unicode-character-except-"-or-\-or-control-character"
  = [^"\\\0-\x1F\x7f]
  / '\\"'  { return '"';  }
  / "\\\\" { return "\\"; }
  / "\\/"  { return "/";  }
  / "\\b"  { return "\b"; }
  / "\\f"  { return "\f"; }
  / "\\n"  { return "\n"; }
  / "\\r"  { return "\r"; }
  / "\\t"  { return "\t"; }
  / "\\u" digits:$(hexDigit hexDigit hexDigit hexDigit) {
      return String.fromCharCode(parseInt(digits, 16));
    }

hexDigit
  = [0-9a-fA-F]

number
  = sign:("-" {return true})? unsignedvalue:(INTEGER ('.' INTEGER)? {return parseFloat(text());}) {
    if(sign) return -unsignedvalue;
    return unsignedvalue;
  }

INTEGER = [0-9]+ {return parseInt(text());}

ws "whitespace"
  = [ \t\n\r]*
